buildscript {
    ext.kotlin_version = '1.2.0'
    // Use protobuf 2.x because it is smaller
    ext.protobuf_version = '2.6.1'
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
    }
}

allprojects  {
    apply plugin: 'maven'
    apply plugin: 'com.github.ben-manes.versions'
    group = 'net.syncthing.java'
    version = '0.1'
}

subprojects {
    apply plugin: 'kotlin'
    apply plugin: 'idea'
    apply plugin: 'com.google.protobuf'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    compileKotlin.kotlinOptions.jvmTarget = "1.6"

    protobuf.protoc {
        artifact = "com.google.protobuf:protoc:$protobuf_version"
    }

    // Make sure Intellij can find generated protobuf files.
    idea.module.sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/java")

    // Workaround for https://github.com/google/protobuf-gradle-plugin/issues/100
    compileKotlin.dependsOn('generateProto')
    sourceSets.main.kotlin.srcDirs += file("${protobuf.generatedFilesBaseDir}/main/java")

    repositories {
        mavenLocal()
        jcenter()
    }

    dependencies {
        compile "org.apache.commons:commons-lang3:3.7"
	// Can't upgrade to 2.6 because it crashes on Android
        compile "commons-io:commons-io:2.5"
        compile "org.slf4j:slf4j-api:1.7.25"
        compile "ch.qos.logback:logback-classic:1.2.3"
        compile "commons-cli:commons-cli:1.4"
        compile "com.google.code.gson:gson:2.8.2"
        compile "com.google.guava:guava:23.5-android"
        compile "com.google.code.findbugs:jsr305:3.0.2"
        compile "org.apache.httpcomponents:httpclient:4.5.4"
        compile "com.google.protobuf:protobuf-java:$protobuf_version"
        compile "org.bouncycastle:bcmail-jdk15on:1.58"
        compile "net.jpountz.lz4:lz4:1.3.0"
        compile "org.mortbay.jetty.alpn:alpn-boot:8.1.11.v20170118"
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    }
}
