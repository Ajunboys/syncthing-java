buildscript {
    ext.kotlin_version = '1.2.20'
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
    }
}

allprojects {
    apply plugin: 'maven'
    apply plugin: 'com.github.ben-manes.versions'
    group = 'com.github.Nutomic'
    version = '0.1.4'
}


subprojects {
    apply plugin: 'kotlin'
    apply plugin: 'idea'
    apply plugin: 'com.google.protobuf'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    compileKotlin.kotlinOptions.jvmTarget = "1.6"

    task sourceJar(type: Jar) {
        group 'Build'
        description 'An archive of the source code'
        classifier 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourceJar
        archives javadocJar
    }

    protobuf {
        protoc {
            // You still need protoc like in the non-Android case
            //artifact = "com.google.protobuf:protoc:3.5.1-1"
        }
        plugins {
            javalite {
                // The codegen for lite comes as a separate artifact
                artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0"
            }
        }
        generateProtoTasks {
            all().each { task ->
                task.builtins {
                    // In most cases you don't need the full Java output
                    // if you use the lite output.
                    remove java
                }
                task.plugins {
                    javalite { }
                }
            }
        }
    }

    // Make sure Intellij can find generated protobuf files.
    idea.module.sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/javalite")

    // Workaround for https://github.com/google/protobuf-gradle-plugin/issues/100
    compileKotlin.dependsOn('generateProto')
    sourceSets.main.kotlin.srcDirs += file("${protobuf.generatedFilesBaseDir}/main/javalite")

    repositories {
        mavenLocal()
        jcenter()
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        compile "com.google.protobuf:protobuf-lite:3.0.1"
    }
}
