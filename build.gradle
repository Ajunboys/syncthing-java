buildscript {
    ext.kotlin_version = '1.2.0'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects  {
    apply plugin: 'maven'
    group = 'net.syncthing.java'
    version = '1.3'
}

subprojects {
    apply plugin: 'kotlin'
    apply plugin: 'idea'
    apply plugin: 'com.google.protobuf'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    compileKotlin.kotlinOptions.jvmTarget = "1.6"

    protobuf.protoc {
        artifact = 'com.google.protobuf:protoc:2.6.1'
    }

    // Make sure Intellij can find generated protobuf files.
    idea.module.sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/java")

    // Workaround for https://github.com/google/protobuf-gradle-plugin/issues/100
    compileKotlin.dependsOn('generateProto')
    sourceSets.main.kotlin.srcDirs += file("${protobuf.generatedFilesBaseDir}/main/java")

    repositories {
        mavenLocal()
        maven { url "http://repo.maven.apache.org/maven2" }
    }

    dependencies {
        compile "org.apache.commons:commons-lang3:3.4"
        compile "commons-io:commons-io:2.4"
        compile "org.slf4j:slf4j-api:1.7.13"
        compile "ch.qos.logback:logback-classic:1.1.3"
        compile "commons-cli:commons-cli:1.3.1"
        compile "com.google.code.gson:gson:2.5"
        compile "com.google.guava:guava:19.0"
        compile "com.google.code.findbugs:jsr305:3.0.0"
        compile "org.apache.httpcomponents:httpclient:4.3.5"
        compile "com.google.protobuf:protobuf-java:2.6.1"
        compile "org.bouncycastle:bcmail-jdk15on:1.55"
        compile "net.jpountz.lz4:lz4:1.3.0"
        compile "org.mortbay.jetty.alpn:alpn-boot:7.1.3.v20150130"
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    }
}